'----------------------------------------------------------------------------------------------------
'2024/11/27 15:36:44
'----------------------------------------------------------------------------------------------------
'参照設定
'LibraryName:Scripting
'LIBID:{420B2830-E718-11CF-893D-00A0C9054228}
    'ReferenceName:Microsoft Scripting Runtime
    'FullPath(win32):C:\Windows\SysWOW64\scrrun.dll
    'FullPath(win64):C:\Windows\System32\scrrun.dll
    'Major.Minor:1.0
        'CLSID:{0D43FE01-F093-11CF-8940-00A0C9054228}
        'ProgID:Scripting.FileSystemObject
Public Function GetSelectFileSpec(ByVal filePath As String, Optional ByVal dialogTitle As String, _
                                  Optional ByVal filterDescriptions As String, Optional ByVal filterExtensions As String, Optional ByVal filterSeparateFLG As Boolean, _
                                  Optional ByVal multiSelect As Boolean) As String()
On Error GoTo Err

    'ファイルパス 存在確認
    If Not FSO.FolderExists(filePath) Then
        Call ShowErrMsg("指定されたファイルパスが存在しません。", title:="GetSelectFileSpec")
        Exit Function
    End If

    '引数 空白確認
    If dialogTitle = "" Then dialogTitle = "ファイル 選択"
    If filterDescriptions = "" Then filterDescriptions = "すべてのファイル"
    If filterExtensions = "" Then filterExtensions = "*.*"

    Dim filterDescriptionArr() As String, filterExtensionArr() As String
    filterDescriptionArr = Split(filterDescriptions, ",")
    filterExtensionArr = Split(filterExtensions, ",")

    '配列 要素数比較
    Dim filterDescriptionArrUpper As Long, filterExtensionArrUpper As Long
    filterDescriptionArrUpper = UBound(filterDescriptionArr, 1)
    filterExtensionArrUpper = UBound(filterExtensionArr, 1)
    If filterDescriptionArrUpper <> filterExtensionArrUpper Then
        Call ShowErrMsg("指定されたファイルの種類と拡張子の数が一致しません。", title:="GetSelectFileSpec")
        Exit Function
    End If

    Dim filterArrUpper As Long
    filterArrUpper = filterDescriptionArrUpper

    '引数 解析
    Dim filterArrIndx As Long
    For filterArrIndx = 0 To filterArrUpper
        '説明
        If filterDescriptionArr(filterArrIndx) = "" Then filterDescriptionArr(filterArrIndx) = "すべてのファイル"

        '拡張子
        Dim bufArr As Variant, bufArrIndx As Long
        bufArr = Split(IIf(filterExtensionArr(filterArrIndx) = "", "*", filterExtensionArr(filterArrIndx)), ";")
        For bufArrIndx = 0 To UBound(bufArr, 1)
            If bufArr(bufArrIndx) = "" Then bufArr(bufArrIndx) = "*"
        Next bufArrIndx
        filterExtensionArr(filterArrIndx) = "*." & Join(bufArr, ";*.")
    Next filterArrIndx

    'フィルター分割 確認
    If Not filterSeparateFLG Then
        filterDescriptionArr(0) = Join(filterDescriptionArr, ",")
        ReDim Preserve filterDescriptionArr(0)
        filterExtensionArr(0) = Join(filterExtensionArr, ";")
        ReDim Preserve filterExtensionArr(0)

        filterArrUpper = 0
    End If

    'ダイアログボックス 設定
    With Application.FileDialog(msoFileDialogFilePicker)
        With .Filters
            Call .Clear
            For filterArrIndx = 0 To filterArrUpper
                Call .Add(filterDescriptionArr(filterArrIndx), filterExtensionArr(filterArrIndx))
            Next filterArrIndx
        End With
        .FilterIndex = 1
        .InitialFileName = FSO.BuildPath(filePath, Application.PathSeparator)
        .Title = dialogTitle
        .AllowMultiSelect = multiSelect

        'ファイル選択状態 判定
        If .Show = -1 Then
            Dim selectedArr() As String
            If multiSelect Then
                ReDim selectedArr(.SelectedItems.Count - 1)

                Dim indx As Long
                For indx = 1 To .SelectedItems.Count
                    selectedArr(indx - 1) = .SelectedItems(indx)
                Next indx

                GetSelectFileSpec = selectedArr
            Else
                ReDim selectedArr(0)

                selectedArr(0) = .SelectedItems(1)

                GetSelectFileSpec = selectedArr
            End If
        End If

    End With

    Exit Function

'エラー処理
Err:

    Call ShowErrMsg(Err.Description, Err.Number, "GetSelectFileSpec")

End Function
'----------------------------------------------------------------------------------------------------